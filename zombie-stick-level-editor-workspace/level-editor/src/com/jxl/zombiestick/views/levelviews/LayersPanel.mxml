<?xml version="1.0" encoding="utf-8"?>
<s:Scroller xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" 
		 width="300" height="100%" xmlns:layersviews="com.jxl.zombiestick.views.levelviews.layersviews.*">
	
	<fx:Script>
		<![CDATA[
			import com.jxl.zombiestick.events.view.LayersPanelEvent;
			import com.jxl.zombiestick.vo.GameObjectVO;
			import com.jxl.zombiestick.vo.LevelVO;
			
			import mx.collections.ArrayCollection;
			import mx.events.CollectionEvent;
			
			[Bindable]
			public var level:LevelVO;
			
			[Bindable]
			public var selectedIndices:Vector.<int>;
			
			private var _selections:ArrayCollection;
			private var selectionsDirty:Boolean = false;
			
			public function get selections():ArrayCollection { return _selections; }
			public function set selections(value:ArrayCollection):void
			{
				if(value !== _selections)
				{
					if(_selections)
					{
						_selections.removeEventListener(CollectionEvent.COLLECTION_CHANGE, onSelectionsChanged);
					}
					_selections = value;
					if(_selections)
					{
						_selections.addEventListener(CollectionEvent.COLLECTION_CHANGE, onSelectionsChanged);
					}
					selectionsDirty = true;
					invalidateProperties();
				}
			}
			
			protected override function commitProperties():void
			{
				super.commitProperties();
				
				if(selectionsDirty)
				{
					selectionsDirty = false;
					var indices:Vector.<int> = new Vector.<int>();
					if(_selections)
					{
						var len:int = _selections.length;
						for(var index:int = 0; index < len; index++)
						{
							var gameObject:GameObjectVO = _selections[index] as GameObjectVO;
							var listIndex:int = level.events.getItemIndex(gameObject);
							indices[index] = listIndex;
						}
					}
					selectedIndices = indices;
				}
			}
			
			
			private function onLayersChange():void
			{
				var event:LayersPanelEvent = new LayersPanelEvent(LayersPanelEvent.SELECTIONS_CHANGED);
				event.indices = layersList.selectedIndices;
				dispatchEvent(event);
			}
			
			private function onDeleteSelected():void
			{
				dispatchEvent(new LayersPanelEvent(LayersPanelEvent.DELETE_SELECTED));
			}
			
			private function onSelectionsChanged(event:CollectionEvent):void
			{
				selectionsDirty = true;
				invalidateProperties();
			}
			
		]]>
	</fx:Script>
	
	<s:VGroup width="100%" height="100%">
		<s:Label text="Layers" />
		<s:HGroup>
			<s:Button label="Delete" click="onDeleteSelected()" />
		</s:HGroup>
		<layersviews:LayersList id="layersList"
								dataProvider="{level.events}" 
								width="100%" height="100%" 
								allowMultipleSelection="true"
								selectedIndices="{selectedIndices}"
								change="onLayersChange()"/>
	</s:VGroup>
	
	
</s:Scroller>
